apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

apply from: 'gradle/jacoco.gradle'
apply from: "gradle/utils.gradle"

def KEY_STORE_DIR = "keystore/"
def RELEASE_KEY_STORE_PROPERTIES_FILE_NAME = "keystore_release.properties"
def DEBUG_KEY_STORE_PROPERTIES_FILE_NAME = "keystore_debug.properties"

def keystoreReleaseProperties = loadProps(KEY_STORE_DIR + RELEASE_KEY_STORE_PROPERTIES_FILE_NAME)
def keystoreDebugProperties = loadProps(KEY_STORE_DIR + DEBUG_KEY_STORE_PROPERTIES_FILE_NAME)

def appVersion = [
        major: 1,
        minor: 1,
        patch: 0
]

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.voltek.newsfeed"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "${appVersion.major}.${appVersion.minor}.${appVersion.patch}"
        buildConfigField 'String', 'ApiKey', ApiKey
    }

    signingConfigs {

        debug {
            storeFile rootProject.file(KEY_STORE_DIR + keystoreDebugProperties['storeFile'])
            storePassword keystoreDebugProperties['storePassword']
            keyAlias keystoreDebugProperties['keyAlias']
            keyPassword keystoreDebugProperties['keyPassword']
        }

        release {
            storeFile rootProject.file(KEY_STORE_DIR + keystoreReleaseProperties['storeFile'])
            storePassword keystoreReleaseProperties['storePassword']
            keyAlias keystoreReleaseProperties['keyAlias']
            keyPassword keystoreReleaseProperties['keyPassword']
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles fileTree('proguard').asList().toArray()
            signingConfig signingConfigs.release
        }

        debug {
            testCoverageEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

androidExtensions {
    experimental = true
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlinGradlePlugin"

    // Android support library
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.android.support:design:$versions.supportLibrary"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibrary"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxBindingKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$versions.rxBindingKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$versions.rxBindingKotlin"

    // Dependency injection
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    // Images
    implementation "com.github.bumptech.glide:glide:$versions.glide"

    // Data storage
    implementation "com.orhanobut:hawk:$versions.hawk"
    implementation "com.github.vicpinm:krealmextensions:$versions.kotlinRealmExtensions"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // MVP
    implementation "com.arello-mobile:moxy:$versions.moxy"
    implementation "com.arello-mobile:moxy-app-compat:$versions.moxy"
    kapt "com.arello-mobile:moxy-compiler:$versions.moxy"

    // Animations
    implementation "jp.wasabeef:recyclerview-animators:$versions.recyclerviewAnimators"

    // Unit tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.mockwebserver", {
        exclude group: 'com.squareup.okio', module: 'okio'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$versions.mockitoKotlin", {
        exclude group: 'org.jetbrains.kotlin'
    }
}
