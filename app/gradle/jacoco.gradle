task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = true
    }

    // Exclude Dagger 2 and other generated code
    def fileFilter = [
            // Android
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
            // Dagger 2
            'com.voltek.newsfeed.dagger/**',
            '**/Dagger*.*',
            '**/*Module*.*',
            '**/*Component*.*',
            '**/*_Factory.*',
            '**/*_MembersInjector.*',
            // Moxy
            '**/MoxyReflector*.*',
            // Realm
            '**/*RealmProxy*.*'
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files([
            "${buildDir}/jacoco/testDebugUnitTest.exec",
            fileTree(dir: "${buildDir}/outputs/code-coverage/connected/").matching {
                include '*.ec'
            }
    ])

    // Always regenerate report
    outputs.upToDateWhen { false }
}
